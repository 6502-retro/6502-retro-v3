Name     6502-Retro;
PartNo   00 ;
Date     06/07/2024 ;
Revision 03 ;
Designer Engineer ;
Company  productiondave ;
Assembly None ;
Location  ;
Device   g22v10 ;

/* Input */
Pin 1        = PHI2;
Pin 2        = RW;
Pin [3..10]  = [A15..A8];
Pin 11       = ACIAIRQB;
Pin 13       = VIAIRQB;
Pin 14       = F18AIRQB;

FIELD Address = [A15..A8];

/* Output */
Pin 15 = MEMWE;
Pin 16 = MEMOE;
Pin 17 = IOCS;
Pin 18 = LOWRAMEN;
Pin 19 = HIRAMEN;
Pin 20 = ROMEN;
Pin 23 = IRQB;

/*  Logic */

!ROMEN       = Address:[C000..FFFF];
!HIRAMEN     = Address:[A000..BFFF];
!IOCS        = Address:[9F00..9FFF];
!LOWRAMEN    = Address:[0000..9EFF];

!MEMWE       = PHI2 & !RW;
!MEMOE       = PHI2 & RW;

IRQB          = ACIAIRQB & VIAIRQB & F18AIRQB;
